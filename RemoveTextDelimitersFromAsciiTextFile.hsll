#ifndef __RemoveTextDelimitersFromAsciiTextFile_hsl__
#define __RemoveTextDelimitersFromAsciiTextFile_hsl__ 1

#ifdef HSL_RUNTIME
#pragma once
#include "HSLStrLib/HSLStrLib.hsl"
#endif

#ifndef HSL_RUNTIME
function RemoveTextDelimitersFromAsciiTextFile(
	variable& fileName) variable {return(0);}
#endif

// Removes all text delimiters (") from the given ascii text file.
// Returns nonzero if the given ascii text file was found, otherwise zero.
#ifdef HSL_RUNTIME
function RemoveTextDelimitersFromAsciiTextFile(
	variable& fileName) variable							// [i]: Full path and file name of the ascii text file, e.g. "C:\\Temp\\Test.csv"
{
	variable write(hslFalse);
	object fso;
	object f;
	variable text("");
	variable vbFalse(0);
	variable vbTrue(-1);
	variable forReading(1);
	variable forWriting(2);

	onerror goto Unexpected;
	fso.CreateObject("Scripting.FileSystemObject");
	if (fso.FileExists(fileName))
	{

		f = fso.OpenTextFile(fileName, forReading, vbFalse);
		text = f.ReadAll();
		f.Close();
		fso.DeleteFile(fileName);
		f = fso.OpenTextFile(fileName, forWriting, vbTrue);
		Str::StrReplace(text, "\"", "");
		f.Write(text);
		f.Close();
		return(hslTrue);
	}
	return(hslFalse);

	Unexpected:
	{
		if (!f.IsNull())
			f.Close();
		return(hslFalse);
	}
}
#endif
#endif
// $$author=Henrik$$valid=0$$time=2025-10-16 14:32$$checksum=918c3d6f$$length=083$$
