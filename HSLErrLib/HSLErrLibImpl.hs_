// This is a part of the HSL Error Library.
// Copyright (C) by HAMILTON Bonaduz AG, CH-7402 Bonaduz.
// All rights reserved.
//
// Modification History
// 2001-06-26  bandenmatten : Removed onerror goto statements from library functions.
// 2001-07-17  bandenmatten : Divided library into interface and implemenation.
// 2001-08-08  bandenmatten : Added function ErrRaiseLast.
// 2002-12-05  bandenmatten : Specified return values for exported library functions.
// 2003-01-30  bandenmatten : Moved the included libraries into the #ifdef HSL_RUNTIME block (optimization and exportcontrol).
// 2005-11-11  bandenmatten : Fixed error in function ErrMinor().

#ifndef __HSLErrLibImpl_hsl__
#define __HSLErrLibImpl_hsl__	1

#pragma once //use either pragma once or include guard, not both
#include "HSLMthLib/HSLMthLib.hsl"
#include "HSLStrLib/HSLStrLib.hsl"

namespace HSLErrLib
{
function ErrGetId() variable
	{
		return(err.GetId());
	}

function ErrSetId(variable number) void
	{
		err.SetId(number);
	}

function ErrGetDescription() variable
	{
		return(err.GetDescription());
	}

function ErrSetDescription(variable& description) void
	{
		err.SetDescription(description);
	}

function ErrGetDataAt(variable index) variable
	{
		variable data[];
		data = err.GetData();
		if (0 == index)
			return(data.GetSize());
		else
			return(data.GetAt(index - 1));
	}
	
function ErrSetDataAt(variable index, variable& variableObj) void
	{
		variable data[];
		data = err.GetData();
		if (0 == index)
			data.SetSize(variableObj);
		else
			data.SetAt(index - 1, variableObj);
		err.SetData(data);
	}

function ErrRaise(variable number, variable& description) void
	{
		err.Raise(number, description);
			// Don't catch errors 
	}
	
function ErrRaiseLast() void
	{
		err.Raise();
			// Don't catch errors 
	}
	
function ErrClear() void
	{
		err.Clear();
	}

// Extracts the major number out of an HxResult 
function ErrMajor(variable hxr) variable
	{
		return(MthShiftRight(hxr & 0x0FFFFFFF, 16) & 0x00FF);
	}

// Extracts the minor number out of an HxResult 
function ErrMinor(variable hxr) variable
	{
		return(MthShiftRight(hxr & 0x0FFFFFFF, 24) & 0x1F);
	}

// Extracts the error code out of an HxResult 
function ErrCode(variable hxr) variable
	{
		return((hxr) & 0xFFFF);
	}

// Displays an error in a message box 
function ErrDisplayError(variable& title) variable
	{
		variable style, id;
		string message;
		style = hslError;
		id = err.GetId();
		message = "ERR: ";
		message = message + StrHexIStr(ErrMajor(id));
		message = message + ", ";
		message = message + StrHexIStr(ErrMinor(id));
		message = message + ", ";
		message = message + StrHexIStr(ErrCode(id));
		message = message + "\n\n";
		message = message + err.GetDescription();
		return(MessageBox(message, title, style));
	}
}

#endif
// $$author=Henrik$$valid=0$$time=2025-08-15 14:30$$checksum=898dda1a$$length=083$$
